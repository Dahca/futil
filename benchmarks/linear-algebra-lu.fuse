// BEGIN macro definition



// END macro definition

decl A: ubit<32>[8][8];

for (let i: ubit<4> = 0..8) {
  let j = (0 as ubit<4>);
  while (j < i) {
    decor "#pragma HLS loop_tripcount min=0 max=7 avg=4"
    let k: ubit<4> = (0 as ubit<4>);
    while(k < j) {
      decor "#pragma HLS loop_tripcount min=0 max=6 avg=2"
      let A_i_j = A[i][j];
      ---
      let A_i_k = A[i][k];
      ---
      let A_k_j = A[k][j];
      ---
      A[i][j] := A_i_j - A_i_k * A_k_j;

      // Update loop counter
      k := k + (1 as ubit<4>);
    }
    ---
    let A_i_j = A[i][j];
    ---
    let A_j_j = A[j][j];
    ---
    A[i][j] := A_i_j / A_j_j;

    // Update loop counter
    j := j + (1 as ubit<4>);
  }
  ---
  j := i;
  while (j < (8 as ubit<4>)) {
    decor "#pragma HLS loop_tripcount min=1 max=8 avg=4"
    let k = (0 as ubit<4>);
    while (k < i) {
      decor "#pragma HLS loop_tripcount min=0 max=7 avg=2"
      let A_i_j = A[i][j];
      ---
      let A_i_k = A[i][k];
      ---
      let A_k_j = A[k][j];
      ---
      A[i][j] := A_i_j - A_i_k * A_k_j;
      // Update loop counter
      k := k + (1 as ubit<4>);
    }
    // Update loop counter
    j := j + (1 as ubit<4>);
  }
}
